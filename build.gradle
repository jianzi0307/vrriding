group 'com.pkuvr'
version '1.0-SNAPSHOT'

apply plugin: 'idea'
apply plugin: 'java'

sourceCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    mybatisGenerator
}

dependencies {
    mybatisGenerator 'org.mybatis.generator:mybatis-generator-core:1.3.2'
    mybatisGenerator 'mysql:mysql-connector-java:5.1.17'
    mybatisGenerator 'tk.mybatis:mapper:3.3.2'

    compile group: 'asm', name: 'asm', version: '3.3.1'
    compile group: 'com.jolbox', name: 'bonecp', version: '0.7.1.RELEASE'
    compile group: 'cglib', name: 'cglib', version: '2.2.2'
    compile group: 'commons-pool', name: 'commons-pool', version: '1.5.6'
    compile group: 'commons-logging', name: 'commons-logging', version: '1.1.1'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    compile group: 'commons-httpclient', name: 'commons-httpclient', version: '3.1'
    compile group: 'commons-dbcp', name: 'commons-dbcp', version: '1.4'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.6'
    compile group: 'commons-io', name: 'commons-io', version: '2.0.1'
    compile group: 'concurrent', name: 'concurrent', version: '1.3.4'
    compile group: 'dom4j', name: 'dom4j', version: '1.6.1'
    compile group: 'net.sf.ehcache', name: 'ehcache-core', version: '2.5.2'
    compile group: 'com.google.guava', name: 'guava', version: 'r09'
    compile group: 'org.codehaus.jackson', name: 'jackson-core-lgpl', version: '1.5.2'
    compile group: 'org.codehaus.jackson', name: 'jackson-mapper-lgpl', version: '1.5.2'
    compile group: 'javolution', name: 'javolution', version: '5.5.1'
    compile group: 'jaxen', name: 'jaxen', version: '1.1'
    compile group: 'jcs', name: 'jcs', version: '1.3'
    compile group: 'redis.clients', name: 'jedis', version: '2.1.0'
    compile group: 'org.json', name: 'json', version: '20160212'
    compile group: 'org.mybatis', name: 'mybatis', version: '3.3.0'
    compile group: 'org.mybatis', name: 'mybatis-ehcache', version: '1.0.0'
    compile group: 'org.mybatis', name: 'mybatis-spring', version: '1.2.2'
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.17'
    compile group: 'io.netty', name: 'netty', version: '3.5.7.Final'
    compile group: 'org.springframework', name: 'spring-test', version: '3.0.6.RELEASE'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '2.4.1'
    compile group: 'org.quartz-scheduler', name: 'quartz', version: '1.7.3'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.6.4'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.6.4'
    compile group: 'org.springframework', name: 'spring-context-support', version: '3.0.6.RELEASE'
    compile group: 'org.springframework', name: 'spring-tx', version: '3.0.6.RELEASE'
    compile group: 'org.springframework', name: 'spring-orm', version: '3.0.6.RELEASE'
    compile group: 'org.springframework', name: 'spring-jdbc', version: '3.0.6.RELEASE'
    compile group: 'org.springframework', name: 'spring-context', version: '3.0.6.RELEASE'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

def getDbProperties = {
    def properties = new Properties()
    file("src/main/resources/jdbc-test.properties").withInputStream { inputStream ->
        properties.load(inputStream)
    }
    properties;
}

task mybatisGenerate << {
    def properties = getDbProperties()
    ant.properties['targetProject'] = projectDir.path
    ant.properties['driverClass'] = properties.getProperty("jdbc.driverClass")
    ant.properties['connectionURL'] = properties.getProperty("jdbc.url")
    ant.properties['userId'] = properties.getProperty("jdbc.user")
    ant.properties['password'] = properties.getProperty("jdbc.password")
    ant.properties['src_main_java'] = sourceSets.main.java.srcDirs[0].path
    ant.properties['src_main_resources'] = sourceSets.main.resources.srcDirs[0].path
    ant.properties['modelPackage'] = this.modelPackage
    ant.properties['mapperPackage'] = this.mapperPackage
    ant.properties['sqlMapperPackage'] = this.sqlMapperPackage
    ant.taskdef(
            name: 'mbgenerator',
            classname: 'org.mybatis.generator.ant.GeneratorAntTask',
            classpath: configurations.mybatisGenerator.asPath
    )
    ant.mbgenerator(overwrite: true,
            configfile: 'src/main/resources/generator/generatorConfig.xml', verbose: true) {
        propertyset {
            propertyref(name: 'targetProject')
            propertyref(name: 'driverClass')
            propertyref(name: 'connectionURL')
            propertyref(name: 'userId')
            propertyref(name: 'password')
            propertyref(name: 'src_main_java')
            propertyref(name: 'src_main_resources')
            propertyref(name: 'modelPackage')
            propertyref(name: 'mapperPackage')
            propertyref(name: 'sqlMapperPackage')

        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'com.pkuvr.game_server.main.CrossServerMain'
    }
}